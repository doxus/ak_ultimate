% !TeX spellcheck = pl_PL
\section{Sparc}
	\subsection{Laborka: min, max oraz max - min}
		\subsubsection{Funkcja w języku C}
			\begin{lstlisting}[language=C]
				#include <stdio.h>
				extern int minmax(int *tab, int n, int *max, int *min);
				
				int main()
				{
					int i, N, *tab;
					int max, min, span;
					scanf("%i", &N);	
					if (N < 0) {
						printf("N < 0!\n");
						return -1;
					}
					tab = malloc(N*sizeof(*tab));
					for(i = 0; i < N; ++i)
						scanf("%i", tab + i);
					span = minmax(tab, N, &max, &min);
					printf("min = %i, max = %i, span = %i\n",
					min, max, span);
					free(tab);
					return 0;
				}
			\end{lstlisting}
		\newpage
		\subsubsection{Odpowiednik w SPARCu}
			\begin{lstlisting}[language={[sparc]Assembler}]
				.global minmax
				.proc 4
				; rejestry:
				; %i0 - adres do tablicy
				; %i1 - ilosc liczb (N)
				; %i2 - adres do max
				; %i3 - adres do min
				;
				; %l6 - pomocnicza do przechowania przesuniecia w bajtach
				; %l7 - pomocnicza do porownywania z min/max
				; %l1 - max
				; %l2 - min
				minmax:
					save %sp, -96, %sp ; przesuniecie okienka
					; zaladuj wartosci dla max i min, gdy n <= 0
					mov 0, %l1
					mov 0, %l2
					; sprawdz czy n > 0
					subcc %i1, 1, %i1
					blt end
					nop
					; zaladuj startowe max (%l1) i min(%l2) z pierwszej liczby
					ld [%i0], %l1
					mov %l1, %l2
				petla:
					; sprawdz koniec petli
					blt end
					; wylicz adres i zaladuj kolejna liczbe
					smul %i1, 4, %l6
					ld [%i0+%l6], %l0	; %l0 - obecna liczba
				
					; update max
					subcc %l0, %l1, %l7	; %l1 - max
					blt next
					nop
					mov %l0, %l1
				next:
					; update min
					subcc %l0, %l2, %l7	; %l2 - min
					bgt next2
					nop
					mov %l0, %l2
				
				next2:
					ba petla
					subcc %i1, 1, %i1
				
				end:
					; zapisz wynik
					st %l1, [%i2]
					st %l2, [%i3]
					sub %l1, %l2, %i0
					ret
					restore ; odtworzenie okienka
			\end{lstlisting}
		
	\newpage
		\subsection{Treść}
			Funkcja zwraca \emph{a(n)} wyliczoną ze wzoru rekurencyjnego, pobiera dwa argumenty: \emph{n} oraz \emph{k}, obydwa typu \textit{unsigned int}.
			$$ a(n)=a(n-1)^k+n\cdot k,\;\;\;\;a(0)=1,\;\;\;n=1,2,3,... $$
		\subsubsection{Rozwiązanie nr 1 by Doxus}
			\begin{lstlisting}[language={[sparc]Assembler}]
				.global _start
				
				_start:
					MOV		0x05,	%g1					;! g1 - K
					MOV		0x0A,	%o7					;! rej o7 i i7 -> N (lokalne)
					MOV		%o7,	%g7					;! N absolutne
				
				_petla:
					SAVE	%sp,	-96,	%sp			;! otworzenie okna
				
					SUBcc	%i7,	0x00,	%g0			;! sprawdzenie, czy to dno
					BE		_nzero
					NOP
					
					SUB		%i7,	0x01,	%o7			;! wykonanie rekurencji
					BA		_petla
					NOP
					
				_nzero:
					MOV		0x00,	%i5
					MOV		0x00,	%g2					;! g2 temp n
				_petlapowrot:
					RESTORE								;! zamknięcie koła
					
					MOV		%i5,	%l0					;! obliczenia
					MOV		0x01,	%l1					;! temp k
				_petlamnoz:
					UMUL	%i5,	%l0,	%l0			;! obliczenia zgodnie ze wzorem
					ADD		%l1,	0x01,	%l1
					SUBcc	%l1,	%g1,	%g0
					BNE		_petlamnoz
					
					UMUL	%g2,	%g1,	%l2
					ADD		%l1,	%l2,	%o5
				
					ADD		%g2,	0x01,	%g2
					
					SUBcc	%g2,	%g7,	%g0			;! czy koniec odkręcania koła
					BLE		_petlapowrot
					NOP
				
					MOV		%i5,	%g1					;! g1 - wynik koncowy
					NOP									;! kuniec
			\end{lstlisting}
		\newpage
		\subsubsection{Rozwiązanie nr 2 by Trimack}
			\begin{lstlisting}[language={[sparc]Assembler}]
				.global _start
				.proc 4
				;! Rejestry:
				;! %i0 - n. Numer elementu ciągu, który chcemy pobrać
				;! %i1 - k. Parametr równania ciągu
				;!
				;! %l0 - zmienna tymczasowa do porównań
				;! %l1 - licznik potęgowania
				;! %l2 - wynik potęgowania
				;! %l3 - wynik mnożenia n * k
				;!
				;! Wzór: a(n) = a(n - 1) ^ k + n * k; a(0) = 1
				;! Wartość zwracana: a(n)
				
				_start:
					save	%sp,	-96,	%sp 	;! Przesuniecie okienka
					
					subcc	%i0,	1,		%o0		;! %o0 -> n - 1. if (n == 0)
					bneg	zwroc1
					nop
					
					subcc	%i1,	1,		%l0		;! if (k == 0)
					bneg	zwroc1
					nop
					
					mov		%i1,	%o1				;! %o1 -> k
					call	_start
					nop
				
					;! %o0 -> a(n - 1)
					mov		%i1,	%l1				;! %l1 -> k
					mov		%o0,	%l2				;! %l2 -> a(n - 1)
				
				power:
					;! dekrementacja licznika i sprawdzenie, czy skończyliśmy potęgować: if (%l1 - 1 = 0)
					subcc	%l1,	1,		%l1		
					be		powerEnd
					nop
					umul	%l2,	%o0,	%l2			;! %l2 *= a(n - 1)
					ba		power
					nop
				powerEnd:
					;! %l2 -> a(n - 1) ^ k
					umul	%i0,	%i1,	%l3			;! %l3 = n * k
					add		%l2,	%l3,	%i0			;! %i0 = wynik
					ba		end
					nop
				zwroc1:
					mov		1,		%i0
				end:	
					ret
					restore
			\end{lstlisting}
		
	\newpage
		\subsection{Treść}
			Funkcja realizująca operację w języku C:
			\begin{lstlisting}[language=C]
			int f(int *tab, int n) 
			{
				int i, suma = 0;
				for(i = 0; i < n; i++)
				{
					suma += i*tab[i];
				}
				return suma;
			}
			\end{lstlisting}
		\subsubsection{Rozwiązanie 1}
			\begin{lstlisting}[language={[sparc]Assembler}]
			.global _start
			_start:
			;! i1 wskaznik na pocz tablicy
			;! i2 n
			;! i0 - wyjsciowa suma (RESTORE spowoduje ze bedzie to w rej.
			;! wyjsciowych funkcji nadrzędnej
			;! l0, l1 - wskaznik na el. tablicy, n
			;! l2 - iterator
			;! l3 - suma
				mov		%i1,	%l0			
				mov		%i2,	%l1			
				mov		0x00,	%l2
				mov		0x00,	%l3
			_loop:
				;! if sprawdzają czy i < n
				subcc	%l2,	%l1,	%g0
				bge		_koniec
				nop
			
				ld		[%l0],	%l7
				umul	%l7, 	%l2,	%l7
				add		%l7,	%l3, 	%l3
				
				add		%l2,	0x01,	%l2
				
				ba		_loop
				add		%l0,	0x04,	%l0
				;!można zamienić miejscami i dać nop, ale tak optymalniej
			
			_koniec:
				mov		%l3,	%i0
			\end{lstlisting}
		\newpage
		\subsubsection{Rozwiązanie 2}
			\begin{lstlisting}[language={[sparc]Assembler}]
				.global f
				.proc 4
				;! Rejestry:
				;! %i0 - adres tablicy wejsciowej
				;! %i1 - rozmiar tablicy (n)
				;!
				;! %l0 - zmienna tymczasowa do porównań
				;! %l1 - suma
				;! %l2 - licznik (i)
				;! %l3 - i * a[i]
				;!
				;! int f(int *tab, int n)
				
				f:
					save	%sp,	-96,	%sp
					mov	0,	%l1						;! suma = 0
					mov	0,	%l2						;! i = 0
				
					subcc	%i1,	1,		%l0		;! if (n == 0)
					bneg	koniec
					nop
				
				petlaFor:	
					subcc	%l2,	%i1,	%l0		;! if (i >= n)
					bge		koniec
					nop
				
					ld		[%i0],	%l3				;! %l3 = a[i]
					smul	%l3,	%l2,	%l3		;! %l3 = i * a[i]
					
					add		%l1,	%l3,	%l1		;! suma += i * a[i]
					add		%i0,	4,		%i0		;! %i0 wskazuje na kolejny e. tablicy
					
					add		%l2,	1,		%l2		;! i++
					ba		petlaFor
					nop
				
				koniec:
					mov		%l1,	%i0				;! zwrócenie wyniku
					ret
					restore
			\end{lstlisting}